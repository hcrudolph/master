\subsection{Reachability Queries}

Formal verification of \textsc{ProVerif} models evolves around events.
Events are defined points in the protocol execution that mark certain states of interest.
During verification, the software verifies whether these can be successfully reached and in what number and order they correspond to each other.

Reachability queries check whether a certain state can actually be obtained during a protocol run.
Events can also be combined using the logical operators AND ({\sffamily{\&\&}}) and OR ({\sffamily{||}}) to test whether a certain combination is reachable or not.
Due to this nature, they are used in conjuction with the created model mainly to prove the absence of undesired events or a combination of certain events that is undesired.

\subsubsection{Debug Queries}
\label{ssec:debug}

A common mistake in formal verification is modeling system in such a way that parts or even the whole protocol are not executed at all, leading to erroneous results that convey a false sense of security.
For this reason, simple reachability queries have been defined for all defined events in the protocol, merely verifying whether each event is individually reachable.
If this is not the case, this indicates that the protocol execution stops prior to the specified point and never reaches the desired state.
These debug queries are defined in the format {\sffamily{query event(e)}}, where {\sffamily{e}} is the respective event under test.
As they are only used for the purposes of validating the correctness of the created model and all follow the same pattern, they are not depicted hereafter.
Important to highlight is that all events in the model are indeed reachable, meaning there are no protocol branches which can never be executed.

\subsubsection{Secrecy Queries}
\label{ssec:secrecy}

\textsc{ProVerif} secrecy queries are essentially reachability queries on terms rather than events.
They verify whether the Dolev-Yao adversary is able to successfully compute a certain value using public information.
As detailed in the user manual document, \textsc{ProVerif} offers two ways of specifying such queries -- using the keyword {\sffamily{attacker}} or using the keyword {\sffamily{secret}}.
The former checks ,,whether the attacker can compute the term M, built from free names'' (\cite{blanchet2020proverif}, p.~55), while the latter is used to express queries that test the secrecy of the variable or bound name specified.
Since the created model uses special names that hold all confidential information during protocol execution, only the second variant is used.

There are four types of data in the \gls{prins} protocol which are crucially confidential: private keys of both \glspl{sepp} and \gls{ipx} nodes, master session keys exported from the N32-f \gls{tls} session, session keys derived from them, and the confidential contents in N32-f messages.
As described in section~\ref{sec:issues}, \gls{tls} connections are assumed to be secure and thus, there is no need for the protocol to consider private and public keys of the communicating \glspl{sepp}.

Listing \ref{lst:secrecy-queries} below shows all secrecy queries defined for the \gls{prins} protocol.
Initially, the above-mentioned information elements are defined as global variables assumed to be private, i.e. not available to the adversary.
It should be noted that although this property is assumed to be true, \textsc{ProVerif} does verify the secrecy of these names and fails with an error message should it be able to disprove the assumption (\cite{blanchet2020proverif}, p.~111).

\begin{lstlisting}[caption={Secrecy Queries},label={lst:secrecy-queries},firstnumber=224]
free privkey_ipx_a, privkey_ipx_b: privkey [private].
free par_req_key_a, par_res_key_a, rev_req_key_a, rev_res_key_a,
    par_req_key_b, par_res_key_b, rev_req_key_b, rev_res_key_b,
    master_key_a, master_key_b: key [private].
free conf: bitstring [private].

(* ipx key confidentiality *)
query secret privkey_ipx_a [reachability];
    secret privkey_ipx_b [reachability].

(* master & session key confidentiality *)
query secret master_key_a [reachability];
    secret master_key_b [reachability];
    secret par_req_key_a [reachability];
    secret par_res_key_a [reachability];
    secret rev_req_key_a [reachability];
    secret rev_res_key_a [reachability];
    secret par_req_key_b [reachability];
    secret par_res_key_b [reachability];
    secret rev_req_key_b [reachability];
    secret rev_res_key_b [reachability].

(* sec property 7: message confidentiality *)
query secret conf [reachability].
\end{lstlisting}

As they are the cryptographic basis for authenticating message modifications, the secrecy of \gls{ipx} providers' private keys is a crucial prerequisite for ensuring only legitimate intermediaries may perform changes.
Hence, proving unreachability of {\sffamily privkey\_ipx\_a} and {\sffamily privkey\_ipx\_b} to the adversary in the created model is the goal of the first two secrecy queries in lines TODO.
As these variables are only ever consumed by the \gls{ipx} providers themselves and do not get sent across any channel whatsoever, this is a fairly trivial assertion and indeed, \textsc{ProVerif} is able to successfully prove unreachability to the attacker.

The next secrecy queries concern the master keys and session keys used by both \glspl{sepp} for the N32-f \gls{aead} encryption operations.
While the compromise of a session key would only directly result in a lack of confidentiality for part of the communication --due to cryptographic separation based on the sending or receiving role of the \gls{sepp} as well as the transported message being a request or response--, a master key compromise would allow the adversary to derive the complete hirarchy of cryptographic keys and \gls{iv}.
As described previously, master keys are created using the private constructor {\sffamily deriveMasterKey}.
Afterwards, they are only used for further derivation of cryptographic material and never sent on a channel.
In contrast, session keys derived from these master keys are stored in the private tables of each of the \glspl{sepp} and used subsequently by the {\sffamily aeadEncrypt} and {\sffamily aeadDecrypt} destructors.
Based on the created model, \textsc{ProVerif} is able to successfully prove that adversaries are unable to compute any of this cryptographic material, thereby validating an important prerequisite for N32-f message confidentiality.

Lastly, above query {\sffamily query secret conf [reachability]} explicitly validates confidentiality of the special variable {\sffamily conf} which stores sensitive contents of signaling messages as per the encryption policy.
This corresponds to security property 7 in section~\ref{sec:n32} -- the confidentiality of information contained in the {\sffamily dataToIntegrityProtectAndCipher} block of N32-f messages.
This property, too, is successfully proven by \textsc{ProVerif}, confirming that the \gls{aead} encryption and decryption operations are modeled in a way that preserves confidentiality between \glspl{sepp}.

In conclusion, the above queries are able to prove on the basis of the created model that all information requiring secrecy in the \gls{prins} protocol is indeed never acccessible to the adversary.

\subsubsection{Dishonest participants}

In order to explicitly test some of the security properties listed in section~\ref{ssec:prins-sequence}, several protocol agents are modeled that intentionally violate the intended protocol behavior.
These misbehaving agents comprise sending and receiving processes of the rogue \gls{ipx} providers R1, R2, and R3.

Security property 8 states that intermediaries shall not be able to remove previously added message modifications.
In order to prove this requirement, the \gls{ipx} process {\sffamily N32fRecvIpxR1} is introduced, which violates the protocol in by doing exactly that.
Similar to the process {\sffamily N32fRecvIpxA} shown in listing~\ref{lst:ipx-recv}, the only difference between the two is that \gls{ipx} R1 replaces the existing patch object with the empty patch.
The query in listing~\ref{lst:query-sec-8} below asserts that the events {\sffamily recvValidIpxPatchSeppA} and {\sffamily ipxRecvR1} --given the same N32-f context ID, message ID, \gls{jwe} tag, and \gls{json} patch-- are not reachable together.
That is, all messages from by \gls{ipx} R1 never result in a patch being recognized as valid by the receiving \gls{sepp}.

\textsc{ProVerif} is able to disprove this query, returning a negative result.
This means both states can indeed by reached together in the same execution of proverif.
Trying to export a representation of the discovered trace, however, results in non-termination of the program.
This is not due to a limitation of \textsc{ProVerif} itself but of the \textit{Graphviz} software, specifically its \textit{dot} utility used for drawing directed graphs of the attack trace.
Due to the large number of protocol states, the program is unable to finalize the trace even after several hours of execution.

At this point, it should be noted that not all attacks \textsc{ProVerif} discovers are indeed applicable or practical in real-world scenarios.
Since the attack trace cannot be produced, there is a chance the program finds a wrong attack.
One possible scenario that may be discovered as an attack, since the query does not take into account any temporal relation between the two events, is the \gls{sepp} receiving a valid message first, reaching the event {\sffamily recvValidIpxPatchSeppA} and the \gls{ipx} R1 process receiving the same message by the adversary who is replaying the message afterwards.
However, without further details about the trace found by \textsc{ProVerif}, it is not possible to validate this assumption.

\begin{lstlisting}[caption={Query for security property 8},label={lst:query-sec-8},firstnumber=348]
(* sec property 8: removing previous message modifications *)
query ipx_b_id: bitstring, n32f_context: bitstring,
    msg_id: nat, jwe_tag: mac, ipx_a_mods: ipxmod, ipx_b_mods: ipxmod;

    event(recvValidIpxPatchSeppA(
        ipx_b_id, n32f_context, msg_id, jwe_tag, ipx_b_mods)) &&
    event(ipxRecvR1(n32f_context, msg_id, jwe_tag, ipx_a_mods)).
\end{lstlisting}

Listing \ref{lst:query-sec-10} involves the intentionally misbehaving \gls{ipx} provider R3 to verify security property 10.
According to the \gls{3gpp} specification, an adversary on the inter-operator network shall not be able to record message modifications by legitimate \gls{ipx} providers and re-apply them to subsequent messages at a later point in time.
Since intermediary message modifications must contain the \gls{jwe} tag of the original message before being signed, they are only valid for that particular message.
The process {\sffamily N32fRecvIpxR3}, too, takes a similar role as {\sffamily N32fRecvIpxA}.
Connected to both \gls{ipx} B via the channel {\sffamily n32f\_i} and to \gls{sepp} A via the channel {\sffamily n32f\_a}, it tries to re-apply the already signed modifications by \gls{ipx} B before the event {\sffamily ipxRecvR3} is triggered and the modified message is sent towards \gls{sepp} A.

\textsc{ProVerif} proves this query wrong, returning a negative result.
Since the same limitation for the export of the discovered attack trace described above applies as well, it is unclear whether this correspondes to a real attack real.
One potential source of a false attack trace is again the lack of a specified temporal relation, which allows for the possibility that {\sffamily ipxRecvR3} is executed after {\sffamily recvValidIpxPatchSeppA}.

\begin{lstlisting}[caption={Query for security property 10},label={lst:query-sec-10},firstnumber=356]
(* sec property 10: rejecting replayed json patches *)
query ipx_a_id: bitstring, n32f_context: bitstring,
    msg_id: nat, jwe_tag: mac, ipx_mods: ipxmod;

    event(recvValidIpxPatchSeppA(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_mods)) &&
    event(ipxRecvR3(n32f_context, msg_id, jwe_tag, ipx_mods)).
\end{lstlisting}

Listing \ref{lst:query-sec-11} shows the query for security property 11, i.e. the receiving \gls{sepp} only accepting message modifications from known \gls{ipx} providers.
The process of \gls{ipx} R2 behaves similarly to the sending and receiving processes of the genuine \gls{ipx} A in that it consumes N32-f messages, applies exactly the same message modifications, and forwards the message towards either of the \glspl{sepp}.
The only difference is the \gls{ipx} ID and the private key used by \gls{ipx} R2 are not known to the receiving party.

\textsc{ProVerif} successfully proves this property true, returning a positive result.
This means no trace of the protocol model has been found in which \gls{sepp} A or \gls{sepp} B accept modifications written by \gls{ipx} R2.

\begin{lstlisting}[caption={Query for security property 11},label={lst:query-sec-11},firstnumber=364]
(* sec property 11: rejecting unknown ipx providers *)
query ipx_a_id: bitstring, n32f_context: bitstring,
    msg_id: nat, jwe_tag: mac, ipx_a_mods: ipxmod;
    event(recvValidIpxPatchSeppA(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_a_mods)) &&
    event(ipxRecvR2(n32f_context, msg_id, jwe_tag, ipx_a_mods));

    event(recvValidIpxPatchSeppB(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_a_mods)) &&
    event(ipxSendR2(n32f_context, msg_id, jwe_tag, ipx_a_mods)).
\end{lstlisting}

The query in listing~\ref{lst:query-sec-12} tests security property 12, i.e. the receiving \gls{sepp} rejecting \gls{json} patches that contain message modifications not captured in the related modification policy.
If the patch originates from the foreign \gls{sepp}'s \gls{ipx} provider, then the foreign \gls{sepp}'s modification policy applies.
If the patch is written by the directly connected \gls{ipx} provider, the \gls{sepp}'s own modification policy is used.
The sending process of \gls{ipx} R1 behaves similarly as the the genuine sending process of \gls{ipx} A, including the use of the same \gls{ipx} ID and private key for signing message modifications.
However, the modifications written as part of the \gls{json} patch do not match the modification policy the receiving \gls{sepp} uses for verification.

\textsc{ProVerif} is able to successfully validate this property, returning a positive result.
This proves the absence of a trace in which \gls{ipx} R1 writes message modifications and \gls{sepp} B recognizes them as being valid.

\begin{lstlisting}[caption={Query for security property 12},label={lst:query-sec-12},firstnumber=375]
(* sec property 12: rejecting unknown message modifications *)
query ipx_a_id: bitstring, n32f_context: bitstring,
    msg_id: nat, jwe_tag: mac, ipx_mods: ipxmod;

    event(recvValidIpxPatchSeppB(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_mods)) &&
    event(ipxSendR1(n32f_context, msg_id, jwe_tag, ipx_mods)).
\end{lstlisting}

\subsection{Correspondence Assertions}
\label{ssec:correspondence}

Correspondence assertions verify in which order and multiplicity events related to each other.
As outlined previously, they can be used to test integrity and authentication properties such as security properties 1 and 2 in section~\ref{ssec:prins-sequence}.
However, given the assumptions made in modeling the \gls{prins} protocol, these particular requirements are not explicitly addressed.
Since the three main security properties of N32-c communication --mutual authentication, integrity and confidentiality-- are all provided by \gls{tls}, there is no way to meaningfully prove them in the created model.

One of the properties successfully proven using correspondence assertions is security property 3, i.e. the derivation of the same N32-f context ID during the initial N32-c handshake.
Listing \ref{lst:query-sec-3} below shows the query for the same, taking into account both the sending and receiving role for each of the \glspl{sepp}.
Following the exchange of their respective pre-context IDs over N32-c, both peers combine the information using the {\sffamily deriveContextID} constructor with the sending \gls{sepp}'s input first and the receiving \gls{sepp}'s input second.
Afterwards, the events {\sffamily sendN32fContext} and {\sffamily recvN32fContext} are triggered, depending on the \gls{sepp}'s role.
The related queries assert that both \glspl{sepp} only reach these states when the resulting N32-f context IDs are the same.
As the absence of a trace that does not satisfy this condition is successfully proven, it is guarateed that both \gls{sepp} always derive a matching N32-f context ID.

\begin{lstlisting}[caption={Query for security property 3},label={lst:query-sec-3},firstnumber=317]
(* sec property 3: N32-f Context IDs equivalent *)
query sepp_a_addr: bitstring, sepp_b_addr: bitstring,
    n32f_a_pid: bitstring, n32f_b_pid: bitstring,
    n32f_context_a: bitstring, n32f_context_b: bitstring;
    event(recvN32fContext(
        sepp_b_addr, n32f_a_pid, n32f_b_pid, n32f_context_b
    )) &&
    event(sendN32fContext(
        sepp_a_addr, n32f_a_pid, n32f_b_pid, n32f_context_a
    )) ==>
    n32f_context_a = n32f_context_b;

    event(recvN32fContext(
        sepp_a_addr, n32f_b_pid, n32f_a_pid, n32f_context_a
    )) &&
    event(sendN32fContext(
        sepp_b_addr, n32f_b_pid, n32f_a_pid, n32f_context_b
    )) ==>
    n32f_context_a = n32f_context_b.
\end{lstlisting}

The next security property 4 concerns the derivation of master keys.
Both sending and receiving \gls{sepp} shall derive matching keys during the initial N32-c handshake.
\textsc{ProVerif} is able to confirm this requirement using the queries shown in listing~\ref{lst:query-sec-4} below.
As described in section~\ref{ssec:abstractions}, the model represents the \gls{tls} export function by a private constructor.
Due to this over-abstraction, the insights gained into the \gls{prins} protocol itself are rather limited.
Nevertheless, proving the fact that sending and receiving \gls{sepp} only reach the protocol states {\sffamily sendMasterKey} and {\sffamily recvMasterKey} if the resulting master keys are equal does increase the confidence in the correct behavior of the created model.

\begin{lstlisting}[caption={Query for security property 4},label={lst:query-sec-4},firstnumber=337]
(* sec property 4: master keys equivalent *)
query sepp_a_addr: bitstring, sepp_b_addr: bitstring, n32f_cid: bitstring,
    mkey_a: key, mkey_b: key;
    event(recvMasterKey(sepp_b_addr, n32f_cid, mkey_a)) &&
    event(sendMasterKey(sepp_a_addr, n32f_cid, mkey_b)) ==>
    mkey_a = mkey_b;

    event(recvMasterKey(sepp_a_addr, n32f_cid, mkey_a)) &&
    event(sendMasterKey(sepp_b_addr, n32f_cid, mkey_b)) ==>
    mkey_b = mkey_a.
\end{lstlisting}

Listing \ref{lst:query-sec-6} shows a correspondence query addressing multiple protocol requirements at the same time.
Security properties 6 and 7 require all N32-f messages to be authenticated and all message contents to be integrity protected.

The events {\sffamily sendN32fMsgSeppB} and {\sffamily recvN32fMsgSeppA} as well as {\sffamily sendN32fMsgSeppA} and {\sffamily recvN32fMsgSeppB} depicted below should always be executed after another, given the same N32-f context ID, message ID, and message contents.
Moreover, they should demonstrate injective correnspondence, meaning each receiving event is preceeded by exactly one sending event in the foreign \gls{sepp}.
Hence, the two sending events are marked with the keyword {\sffamily inj-event}.
The parameters {\sffamily msg\_conf} and {\sffamily msg\_nonconf} represent the \textit{dataToIntegrityProtectAndCipher} and \textit{dataToIntegrityProtect} objects of an N32-f message.

Attempting to prove this property results in non-termination of \textsc{ProVerif}\footnote{While the program was still responsive and did make occasional progress, the execution of \textsc{ProVerif} was aborted after 100 hours of runtime.}.
This shows one of the limitations of model checking of unbound protocol sessions, described in more detail in the following section: ensuring the verification terminates is not trivial.
In order to be able to address the remaining queries, this particular test has to be removed from the \textsc{ProVerif} file, leaving the integrity protection and authentication of N32-f messages unverified.

\begin{lstlisting}[caption={Query for security property 6 and 7},label={lst:query-sec-6},firstnumber=403]
(* sec property 6/7: integrity protection / authentication *)
query msg_id: nat, n32f_context: bitstring,
    msg_conf: bitstring, msg_nonconf: bitstring;
    event(recvN32fMsgSeppA(
        n32f_context, msg_id, msg_conf, msg_nonconf)) ==>
    inj-event(sendN32fMsgSeppB(
        n32f_context, msg_id, msg_conf, msg_nonconf));

    event(recvN32fMsgSeppB(
        n32f_context, msg_id, msg_conf, msg_nonconf)) ==>
    inj-event(sendN32fMsgSeppA(
        n32f_context, msg_id, msg_conf, msg_nonconf)).
\end{lstlisting}

Listing \ref{lst:query-sec-9} shows the query testing security property 9, i.e. authentication and integrity protection of \gls{ipx} message modifications.
It is defined as an injective correspondence between two successful patch validations in the receiving \gls{sepp} ({\sffamily recvValidIpxPatchSeppA} or {\sffamily recvValidIpxPatchSeppB}) and two sending and receiving \gls{ipx} provider processes ({\sffamily ipxRecvA}, {\sffamily ipxSendB}, {\sffamily ipxRecvB}, and {\sffamily ipxSendA}).
In other words, given the same N32-f context ID and message ID, each successful validation of both \gls{ipx} patches must be preceeded by the directly connected \gls{ipx} provider receiving this message and the peer \gls{sepp}'s \gls{ipx} provider sending the same.

For both of these queries \textsc{ProVerif} returns a negative result, meaning that a trace has been found in which the receiving \gls{sepp} identifying two valid patches from different \gls{ipx} providers is not preceeded by \gls{ipx} A and \gls{ipx} B transmitting the same message.
As for the other discovered attacks, the traces cannot be successfully obtained.

\begin{lstlisting}[caption={Query for security property 9},label={lst:query-sec-9},firstnumber=383]
(* sec property 9: json patch authentication/integrity protection *)
query n32f_context: bitstring, ipx_a_id: bitstring, ipx_b_id: bitstring,
    msg_id: nat, jwe_tag: mac, ipx_a_mods: ipxmod, ipx_b_mods: ipxmod,
    http_method: bitstring, msg_body: bitstring;
    event(recvValidIpxPatchSeppA(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_a_mods)) &&
    event(recvValidIpxPatchSeppA(
        ipx_b_id, n32f_context, msg_id, jwe_tag, ipx_b_mods)) ==> (
            event(ipxRecvA(n32f_context, msg_id, jwe_tag, ipx_a_mods)) &&
            event(ipxSendB(n32f_context, msg_id, jwe_tag, ipx_b_mods))
        );

    event(recvValidIpxPatchSeppB(
        ipx_b_id, n32f_context, msg_id, jwe_tag, ipx_b_mods)) &&
    event(recvValidIpxPatchSeppB(
        ipx_a_id, n32f_context, msg_id, jwe_tag, ipx_a_mods)) ==> (
            event(ipxRecvB(n32f_context, msg_id, jwe_tag, ipx_b_mods)) &&
            event(ipxSendA(n32f_context, msg_id, jwe_tag, ipx_a_mods))
        ).
\end{lstlisting}


\subsection{Observational Equivalence}
\label{ssec:equivalence}

The \gls{3gpp} specification does not cite specific observational equivalence requirements.
Nevertheless, as highlighted by security property 5 in section~\ref{sec:n32}, strong secrecy for the master keys and derived session keys is certainly desireable, meaning the attacker should not be able to detect when any of these parameters change.
\textsc{ProVerif} generally features verification of strong secrecy by use of the {\sffamily noninterf} keyword (\cite{blanchet2020proverif}, p.~55).
However, modeling the \gls{prins} protocol exposes multiple corner cases for which verification of this property is presently not supported.
These shortcomings lead to a failure to successfully verify strong secrecy for the created model of \gls{prins} with the queries shown in listing~\ref{lst:query-sec-5} below.

\begin{lstlisting}[caption={Query for security property 5},label={lst:query-sec-5},firstnumber=419]
(* sec property 5: strong secrecy of master/session keys *)
noninterf master_key_a.
noninterf master_key_b.
noninterf par_req_key_a.
noninterf par_res_key_a.
noninterf rev_req_key_a.
noninterf rev_res_key_a.
noninterf par_req_key_b.
noninterf par_res_key_b.
noninterf rev_req_key_b.
noninterf rev_res_key_b.
\end{lstlisting}

An initial version of the model implements N32-f message IDs as natural numbers.
This allows increasing the sequence counter with every outgoing message and convenient checks whether a certain N32-f message ID was already sent previously.
When attempting to verify this model with \textsc{ProVerif}, the tool outputs the error message \textit{,,Natural numbers do not work with non-interference yet''}.
Hence, any natural number in the model would impede strong secrecy queries.

An alternative variant of the model created due to the above limitation represents N32-f message IDs using bitstrings.
In this case, incrementing the sequence counter within the \gls{sepp} is a matter of calling a trivial constructor consuming a bitstring (the old sequence number) and producing another one (the updated sequence number).
The check for previously sent message IDs can be modeled using a set of bitstrings, which is updated every time a N32-f message is sent out.
A suitable way to determine set membership using \textsc{ProVerif}'s {\sffamily predicate} construct is described in the official user manual (\cite{blanchet2020proverif}, p.~96).
However, program execution using this representation of message IDs yields the following error message: \textit{,,Predicates are currently incompatible with non-interference''}.

Modelling N32-f message IDs as bitstrings does not result in a improvement of the verification result.
The final model of the protocol uses natural numbers, since this resembles real-world implementations more closely and further helps to reduce the amount of code required to model this particular protocol aspect, thereby reducing complexity and the potential for errors.

\subsection{Summary}
\label{ssec:verif-summary}

All in all, the queries described above are only able to successfully verify four out of the 12 security properties listed in section~\ref{ssec:prins-sequence}.
A formal proof for the remaining two thirds cannot be produced due several different reasons.

As the N32-c channel is modeled using a \textsc{ProVerif} private channel without consideration how the same is established renders proofs for security property 1 and 2 superfluous.
Given that \gls{tls} has successfully been proven secure in previous research (see \cite{cremers2017comprehensive}, \cite{van2018analysis}), it is fair to assume that these requirements are met.
The strong secrecy property cannot be proven by \textsc{ProVerif} due to a limitation of the tool itself, regardless of the N32-f message ID being modeled as natural number or bitstring.
Properties 6 and 7 lead to non-termination of \textsc{ProVerif} itself while properties 8, 9, and 10 are explicitly disproven by the program, but a visual represenation of the attack traces cannot be exported due to the large number of nodes and edges in the resulting graph.

Table \ref{tab:verification-result} summarizes the results of the \textsc{ProVerif} queries applied to the created model of \gls{prins}.

\begin{table}[ht]
    \centering
    \begin{tabular}{l|l|l|l}
    Security Property & Query                                           & Result  & Comment                               \\
    \hline
    1                 & None                                            & True    & True by assumption                    \\
    2                 & None                                            & True    & True by assumption                    \\
    3                 & Listing \ref{lst:query-sec-3}  & True    & Successfully proved                   \\
    4                 & Listing \ref{lst:query-sec-4}  & True    & Successfully proved                   \\
    5                 & Listing \ref{lst:query-sec-5}  & Unclear & ProVerif limitation                   \\
    6                 & Listing \ref{lst:query-sec-6}  & Unclear & ProVerif does not terminate           \\
    7                 & Listing \ref{lst:query-sec-6}  & Unclear & ProVerif does not terminate           \\
    8                 & Listing \ref{lst:query-sec-8}  & False   & Disproved, attack trace not available \\
    9                 & Listing \ref{lst:query-sec-9}  & False   & Disproved, attack trace not available \\
    10                & Listing \ref{lst:query-sec-10} & False   & Disproved, attack trace not available \\
    11                & Listing \ref{lst:query-sec-11} & True    & Successfully proved                   \\
    12                & Listing \ref{lst:query-sec-12} & True    & Successfully proved\\
    \hline
    \end{tabular}
    \caption{Verification result of the PRINS security properties}
    \label{tab:verification-result}
\end{table}

While it is positive to successfully prove part of the properties, these results offer limited insights into why the verification of the majority of properties fails or is inconclusive.
One can argue that an attack reconstruction is actually an essential part of a formal verification as to guide potential improvements to the protocol under test.
However, all throughout the verification of the protocol there have been a number of significant challenges in ensuring the model does behave as intended and writing composing queries that produce a meaningful result.
Some of these have been resolved, others remain open as part of this thesis, such as the error N32-c signaling, and may be subject for future work.
The next section details the user experience of \textsc{ProVerif} for this particular project and lessons learned for future verification attempts.